apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        flavorDimensions "versionCode"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    //打包后应用名称
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (outputFile != null && outputFile.name.endsWith('release.apk')) {
                def version = variant.versionName.replace(".", "")
                outputFileName = applicationId + "_${variant.productFlavors[0].resValues.get("app_name").value}" + version + ".apk"
            }
        }
    }

    signingConfigs {

        testappone {
            storeFile file("../jks/TestAppOne.jks")
            storePassword "121123"
            keyAlias "testappone"
            keyPassword "121123"
        }

        testapptwo {
            storeFile file("../jks/TestAppTwo.jks")
            storePassword "121123"
            keyAlias "testapptwo"
            keyPassword "121123"
        }
    }

    buildTypes {
        release {
            minifyEnabled false    //是否打开混淆
            shrinkResources false   //是否打开资源压缩
            zipAlignEnabled true     // Zipalign优化
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
        debug {
            minifyEnabled false    //是否打开混淆
            signingConfig null
        }
    }


    productFlavors {

        TestAppOne {
            applicationId 'cn.mifengkong.testappone'
            versionName '1.0.1'
            versionCode 1
            //应用名
            resValue "string", "app_name", 'AppOne'
            resValue "string", "get_name", 'AppOne'

            //第三方的应用配置也可以放在这里面
            //微信
            buildConfigField "String", "UMENG_SHARE_WEIXIN_ID", "\"wxe1d649292413946e\""
            buildConfigField "String", "UMENG_SHARE_WEIXIN_KEY", "\"99dac6568f9485d8d702b79d86815bcf\""
            //个推
            manifestPlaceholders = [
                    GETUI_APP_ID    : "LRDm4I3goxAvr5lv5is0N2",
                    GETUI_APP_KEY   : "L7YJcRELCG6h4yg1A7UzB9",
                    GETUI_APP_SECRET: "gnYIA0BDol5gI6mzzppus",
                    PACKAGE_NAME    : applicationId,
                    BAIDU_APP_ID    : "mWHP7o3YXmTEGVtMudN0doGCaDjEUT3D"
            ]

            //签名文件
            signingConfig signingConfigs.testappone
        }

        TestAppTwo {
            applicationId 'cn.mifengkong.testapptwo'
            versionName '2.0.1'
            versionCode 2
            //应用名
            resValue "string", "app_name", 'AppTwo'

            //微信
            buildConfigField "String", "UMENG_SHARE_WEIXIN_ID", "\"wxe1d649292413946e\""
            buildConfigField "String", "UMENG_SHARE_WEIXIN_KEY", "\"99dac6568f9485d8d702b79d86815bcf\""
            //个推
            manifestPlaceholders = [
                    GETUI_APP_ID    : "LRDm4I3goxAvr5lv5is0N2",
                    GETUI_APP_KEY   : "L7YJcRELCG6h4yg1A7UzB9",
                    GETUI_APP_SECRET: "gnYIA0BDol5gI6mzzppus",
                    PACKAGE_NAME    : applicationId,
                    BAIDU_APP_ID    : "mWHP7o3YXmTEGVtMudN0doGCaDjEUT3D"
            ]

            //签名文件
            signingConfig signingConfigs.testapptwo
        }


    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
